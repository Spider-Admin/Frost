plugins {
	id "java"
	id "application"
	id "eclipse"
}

repositories {
	mavenCentral()
	maven {
		url "https://jitpack.io"
	}
}

def repoVersion
task buildInfo {
	try {
		def cmd = "git describe --always --dirty --tags"
		def proc = cmd.execute()
		repoVersion = proc.text.trim()
	} catch (IOException e) {
		repoVersion = "unknown-version"
		logger.error("Unable to determine version, use \"{}\" as version!", repoVersion)
		logger.error("Cause: {}", e.getMessage())
	}
}
compileJava.dependsOn buildInfo

jar {
	manifest {
		attributes(
			"Implementation-Version": "${repoVersion}"
		)
	}
}

tasks.withType(JavaCompile) {
	options.release = 17
	options.encoding = "UTF-8"
}

application {
	mainClass = "frost.Frost"
	applicationDefaultJvmArgs = ["-Xmx256M"]
	executableDir = ""
}

dependencies {
	implementation("org.slf4j:slf4j-api:2.0.13")
	implementation("org.slf4j:jul-to-slf4j:2.0.13")
	implementation("ch.qos.logback:logback-core:1.5.6")
	implementation("ch.qos.logback:logback-classic:1.5.6")
	implementation("com.github.Spider-Admin:Perst:v4.40.0")
	implementation("org.bouncycastle:bcprov-jdk18on:1.78.1")
	implementation("com.toedter:jcalendar:1.4")
	testImplementation(platform("org.junit:junit-bom:5.10.3"))
	testImplementation("org.junit.jupiter:junit-jupiter")
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

test {
	useJUnitPlatform()
}
